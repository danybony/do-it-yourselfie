buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.novoda:gradle-build-properties-plugin:0.4.1'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.novoda.build-properties'

buildProperties {
    debugSigningConfig {
        using rootProject.file('team-props/debugSigning.properties')
    }
}

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "net.bonysoft.doityourselfie"
        minSdkVersion 24
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField "Boolean", "IS_STANDALONE", "true"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    signingConfigs.all { signingConfig ->
        signingConfig.ext.from = { buildProperties ->
            signingConfig.storeFile teamPropsFile(buildProperties['storeFile'].string)
            signingConfig.storePassword buildProperties['storePassword'].string
            signingConfig.keyAlias buildProperties['keyAlias'].string
            signingConfig.keyPassword buildProperties['keyPassword'].string
        }
    }

    signingConfigs {
        debug {
            from buildProperties.debugSigningConfig
        }
    }
}

kotlin {
    experimental {
        coroutines 'enable'
    }
}

androidExtensions {
    experimental = true
}

dependencies {
    def withoutV4 = { exclude group: 'com.android.support', module: 'support-v4' }

    implementation project(":authentication")
    implementation project(":communication")

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.android.support:appcompat-v7:27.1.1', withoutV4
    implementation 'com.android.support:support-v4:27.1.1'
    implementation 'com.android.support:support-media-compat:27.1.1', withoutV4
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:0.23.4"

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2', withoutV4
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation "android.arch.lifecycle:runtime:1.1.1"
    implementation "android.arch.core:runtime:1.1.1"
    implementation "android.arch.lifecycle:viewmodel:1.1.1"
    implementation "android.arch.lifecycle:livedata-core:1.1.1"
    implementation "com.google.dagger:dagger:2.16"

    implementation project(":secrets")
    implementation project(":photos")

    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation 'com.github.maayyaannkk:ImagePicker:1.0.1'
    implementation "com.orhanobut:hawk:2.0.1"

    debugImplementation 'com.willowtreeapps.hyperion:hyperion-core:0.9.24'
    debugImplementation 'com.willowtreeapps.hyperion:hyperion-attr:0.9.24'
    debugImplementation 'com.willowtreeapps.hyperion:hyperion-crash:0.9.24'
    debugImplementation 'com.willowtreeapps.hyperion:hyperion-timber:0.9.24'

    debugImplementation 'com.github.Commit451:Hyperion-Chuck:1.0.0'
}

apply plugin: 'com.google.gms.google-services'
